import app.cash.paparazzi.gradle.PaparazziPlugin

buildscript {
    ext {
        application_id = 'com.android.dev.engineer.kotlin.compose'
        version_name = "1.0"
    }

    dependencies {
        classpath "com.willian.gama.gradle:plugin:1.0.0"
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.paparazzi apply false
    alias libs.plugins.hilt apply false
}

def localProperties = new Properties()
localProperties.load(project.rootProject.file("local.properties").newDataInputStream())

apply plugin: "com.willian.gama.gradle.plugin.gradle.code-analysis"

code_analysis_tool {
    sonarProjectKey = application_id
    sonarProjectVersion = version_name
    sonarToken = localProperties.getProperty("sonar_token")
}

subprojects {
    afterEvaluate {
        if (project.hasProperty('android')) {
            android {
                compileSdk 34

                defaultConfig {
                    minSdk 23
                    targetSdk 34
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17
                }

                testOptions {
                    reportDir "${project.layout.buildDirectory.get()}/reports/coverage/androidTest/debug/connected" // It's required for Sonar

                    buildTypes {
                        // Jacoco config: It configures/enables createDebugCoverageReport task to be used in generateCodeCoverage task below
                        debug {
                            testCoverageEnabled = true
                        }
                    }
                }

                // Jacoco - Verify violations: https://docs.gradle.org/current/userguide/jacoco_plugin.html#sec:jacoco_report_violation_rules
//                tasks.register('verifyCodeCoverage', JacocoCoverageVerification) {
//                    sourceDirectories.from = file("${project.projectDir}/src/main/java") // main source set
//                    classDirectories.from = files(JacocoUtil.getKotlinFileTree(project))
//                    executionData.from = fileTree(dir: project.layout.buildDirectory.get(), includes: [
//                            '**/*.exec', // unit tests
//                            '**/*.ec' // ui tests
//                    ])
//
//                    violationRules {
//                        rule {
//                            limit {
//                                minimum = 0.3 // 30%
//                            }
//                        }
//                    }
//                }
            }
        }
    }

    // Paparazzi Google Guava compatibility
    plugins.withType(PaparazziPlugin).configureEach {
        dependencies.constraints {
            testImplementation("com.google.guava:guava") {
                attributes {
                    attribute(
                            TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
                            objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM)
                    )
                }
                because("LayoutLib and sdk-common depend on Guava's -jre published variant. See https://github.com/cashapp/paparazzi/issues/1231#issuecomment-1893104358.")
            }
        }
    }
}

// Gradle wrapper: https://docs.gradle.org/current/userguide/gradle_wrapper.html#customizing_wrapper
tasks.named('wrapper') {
    description = "Gradle wrapper task to update binary"
    gradleVersion = libs.versions.gradle.get()
    distributionType = Wrapper.DistributionType.BIN
}

class JacocoUtil {
    static ConfigurableFileTree getKotlinFileTree(Project project) {
        return project.fileTree(
                // Where generated Kotlin classes are located
                dir: "${project.layout.buildDirectory.get()}/tmp/kotlin-classes/debug",
                // Exclude everything that is not created by you, e.g. created by HILT
                excludes: [
                        '**/BuildConfig.*',
                        '**/*$*',
                        '**/Hilt_*.class',
                        'hilt_**',
                        'dagger/hilt/**',
                        '**/*JsonAdapter.*'
                ]
        )
    }
}